allprojects {
    group 'de.exlll'
    version '2.3.1'
}
subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories { mavenCentral() }

    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.0.3'
        testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.0.3'
        testImplementation group: 'org.junit.platform', name: 'junit-platform-suite-api', version: '1.0.3'
        testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testImplementation group: 'com.google.jimfs', name: 'jimfs', version: '1.1'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'de.exlll'
                artifactId = project.getName()
                version = project.getVersion()

                from components.java
            }
        }
        repositories {
            maven {
                name = "frsRepository"
                url = "https://repo.fantasyrealms.net/releases/"
                credentials {
                    username findProperty("frsRepositoryUsername").toString()
                    password findProperty("frsRepositoryPassword").toString()
                }
            }
        }
    }
}
buildscript {
    if (project.hasProperty("local_script")) {
        apply from: file(local_script + "/buildscript.gradle"), to: buildscript
    }
}
if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.gradle")
}

project(':configlib-core') {
    dependencies { implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0' }
}
project(':configlib-bukkit') {
    repositories { maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' } }
    dependencies {
        implementation project(':configlib-core')
        implementation group: 'org.bukkit', name: 'bukkit', version: '1.12.2-R0.1-SNAPSHOT'
    }
    jar { from { project(':configlib-core').sourceSets.main.output } }
    processResources {
        filesMatching("plugin.yml") {
            expand("version": version)
        }
    }
}
project(':configlib-bungee') {
    repositories { maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } }
    dependencies {
        implementation project(':configlib-core')
        implementation group: 'net.md-5', name: 'bungeecord-api', version: '1.12-SNAPSHOT'
    }
    jar { from { project(':configlib-core').sourceSets.main.output } }
}